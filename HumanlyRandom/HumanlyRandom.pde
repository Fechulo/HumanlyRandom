import android.telephony.SmsManager;

SmsManager smsManager;
Button sendSMSButton;
boolean sendSMSEnabled;
int sentSMSTime;
int timeSinceLastSMS;

void setup() {
  fullScreen();
  orientation(PORTRAIT);
  requestPermission("android.permission.SEND_SMS");
  smsManager = SmsManager.getDefault();
  sendSMSButton = new Button(width/10.8, height-(height/3.84), width-(width/5.4), height/9.6);
  sendSMSEnabled = true;
  strokeWeight(width/216);
  stroke(0);
}

void draw() {
  background(255);
  fill(116, 172, 223); // light blue
  rect(0, 0, width, height/5.647);
  rect(0, height-(height/4.8), width, height/4.8);
  drawInformation();
  sendSMSButton.display();
  
  // handles the pressing and releasing of the button
  if (mousePressed) {
    if (sendSMSButton.isClickable && sendSMSButton.touchingMouse()) {
      sendSMSButton.press();
    } else {
      sendSMSButton.release();
    }
  }
  
  // creates 60 second countdown once button is pressed
  timeSinceLastSMS = (millis() - sentSMSTime)/1000;
  if (timeSinceLastSMS == 60) {
    sendSMSButton.enable();
  }
}

void mouseReleased() {
  if (sendSMSButton.isClickable && sendSMSButton.touchingMouse()) {
    sendSMS();
    sentSMSTime = millis();
    sendSMSButton.disable();
  }
  sendSMSButton.release();
}

// sends me an SMS asking for a random number
void sendSMS() {
  if (hasPermission("android.permission.SEND_SMS")) {
    smsManager.sendTextMessage("+353863781798", null, "Please send a random number", null, null);
  }
}

// draws all the text
void drawInformation() {
  fill(0);
  textAlign(CENTER, CENTER);
  textSize(width/10.8);
  text("Humanly Random", width/2, height/24);
  textSize(width/18);
  text("an SMS-powered\nrandom number generator", width/2, height/8.727);
  textAlign(CENTER, TOP);
  textSize(width/22.5);
  text("According to a study from Shahed University,\n\"Random number generation is a human ability.\nSequences of random numbers generated by\npeople do not follow full randomness criteria.\nThese numbers produced by brain activity\nseem to be completely nonstationary.\"", width/2, height/4.8);
  textSize(width/18);
  text("How it works:", width/2, height/2.157);
  textSize(width/22.5);
  text("When you press the big white button below,\nan SMS is automatically sent to the developer,\nwho will then generate a random number.\nAfter some time, you should receive a text\nmessage containing your humanly-generated\nrandom number", width/2, height/1.9394);
  textSize(width/27);
  text("(Don't forget to check your messages!)", width/2, height-(height/7.68));
  text("Idea & app developed by Federico Handl", width/2, height-(height/15.36));
  text("Random numbers generated by Federico Handl", width/2, height-(height/25.6));
}

class Button {
  float x, y, w, h;
  boolean isClickable;
  boolean isPressed;

  public Button(float x, float y, float w, float h) {
    this.x = x;
    this.y = y;
    this.w = w;
    this.h = h;
    isClickable = true;
    isPressed = false;
  }

  public void display() {
    fill((isPressed || !isClickable)?200:255);
    rect(x, y, w, h, width/21.6);
    fill(0);
    textSize(width/21.6);
    textAlign(CENTER, CENTER);
    text((isClickable)?"Click to generate a random number":"Please wait "+(60-timeSinceLastSMS)+" seconds", x+w/2, y+h/2);
  }

  public boolean touchingMouse() {
    if (mouseX>x && mouseX<x+w && mouseY>y && mouseY<y+h) {
      return true;
    } else {
      return false;
    }
  }

  public void disable() {
    isClickable = false;
  }

  public void enable() {
    isClickable = true;
  }

  public void press() {
    isPressed = true;
  }

  public void release() {
    isPressed = false;
  }
}
